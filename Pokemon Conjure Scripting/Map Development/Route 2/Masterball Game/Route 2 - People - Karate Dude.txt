' This is for the karate dude in the rest stop on Route 2.
' Copyright @2016 Nonibros.
'-----------
' Script Start
'-----------

#dynamic 0x11FFFA0

'---------
' Master Ball Game Function
'---------
#org @masterball
checkflag 0x1100 ' Checks whether we have reached the final object in the order script.
if 0x1 goto @mainfunction ' If we have, just continue on. If not, set up the variables and check.
setvar 0x4002 0X1 'Set our checking variable = the object orders number - 1. Eg. 5th object in line would be 4.
setvar 0x4003 0X2 'Set our checking variable = the object orders number. Eg. 5th object in line would be 5. This is for the next check. This line is Not NEEDED FOR the LAST OBJECT.
call 0X9244000 ' Call the order checking script offset.
goto @mainfunction ' Continue on.

'---------
' Master Ball Game Function End
'---------

'---------
' Main Function
'---------
#org @mainfunction ' This is our main function. Here we call the system and lock/release the player.
lock
faceplayer
random 0x02 ' We will decide on either the neutral message system, or a gender specific system.
compare 0x800D 0x0
if 0x1 goto @neutralcaller ' Go to the neutral function caller.
checkgender
compare 0x800D 0x0 ' Go to the male random message system.
if 0x1 goto @malecaller
compare 0x800D 0x1 ' Go to the female random message system.
if 0x1 goto @femalecaller
release
end

'---------
' Main Function End
'---------

'---------
' Callers
'---------

#org @neutralcaller ' This calls the neutral random message system and then releases the player when finished.
call @neutralrandom
release
end

#org @malecaller ' This calls the male random message system and then releases the player when finished.
call @malerandom
release
end

#org @femalecaller ' This calls the female random message system and then releases the player when finished.
call @femalerandom
release
end

'---------
' Callers End
'---------

'---------
' Neutral Random Message System
'---------

#org @neutralrandom ' We are picking a random number here for the neutral system. Whatever number is picked is the message to display.
random 0x03
compare 0x800D 0x0
if 0x1 goto @neutralprep1
compare 0x800D 0x1
if 0x1 goto @neutralprep2
compare 0x800D 0x2
if 0x1 goto @neutralprep3
goto @neutralprep3 ' If there is a glitch, this will auto go to the last message.
end

'---------
' Neutral Random Message System End
'---------


'---------
' Neutral Messages
'---------

'---------
' Message 1
'---------

#org @neutralprep1
msgbox @neutralmessage1 0x6
return

'---------
' Message 2
'---------

#org @neutralprep2
msgbox @neutralmessage2 0x6
return

'---------
' Message 3
'---------

#org @neutralprep3
msgbox @neutralmessage3 0x6
return

'---------
' Neutral Messages End
'---------

'---------
' Male Random Message System
'---------

#org @malerandom ' We are picking a random number here for the male system. Whatever number is picked is the message to display.
random 0x02
compare 0x800D 0x0
if 0x1 goto @maleprep1
compare 0x800D 0x1
if 0x1 goto @maleprep2
goto @maleprep2 ' If there is a glitch, this will auto go to the last message.
end

'---------
' Male Random Message System End
'---------


'---------
' Male Messages
'---------

'---------
' Message 1
'---------

#org @maleprep1
msgbox @malemessage1 0x6
return

'---------
' Message 2
'---------

#org @maleprep2
msgbox @malemessage2 0x6
return

'---------
' Male Messages End
'---------

'---------
' Female Random Message System
'---------

#org @femalerandom ' We are picking a random number here for the female system. Whatever number is picked is the message to display.
random 0x02
compare 0x800D 0x0
if 0x1 goto @femaleprep1
compare 0x800D 0x1
if 0x1 goto @femaleprep2
goto @femaleprep2 ' If there is a glitch, this will auto go to the last message.
end

'---------
' Female Random Message System End
'---------


'---------
' Female Messages
'---------

'---------
' Message 1
'---------

#org @femaleprep1
msgbox @femalemessage1 0x6
return

'---------
' Message 2
'---------

#org @femaleprep2
msgbox @femalemessage2 0x6
return

'---------
' Female Messages End
'---------


'---------
' Strings
'---------

#org @neutralmessage1
= I am relaxing after an intense\ntraining regimen.

#org @neutralmessage2
= Train hard, chill hard, that's my\nmotto.

#org @neutralmessage3
= I love to train, but I also love\nto relax as well.\pDecisions decisions...

#org @malemessage1
= Young man, you look like you have\na lot of potential.\pBuild up your body, become\nstrong, and you'll be sure to\lsucceed in all that you do!

#org @malemessage2
= I remember when I was your age\nyoung man.\pAh yes, the 7k regimen.

#org @femalemessage1
= Why you sure are a beautiful\nyoung lady.\pYou should still train though.\nIt's good for you.

#org @femalemessage2
= I have a daughter about your age.\nShe is also beautiful.

'---------
' Strings End
'---------


'-----------
' Script End
'-----------