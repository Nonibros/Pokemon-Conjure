' This is for the middle of the rug in the rest stop on Route 2.
' Copyright @2016 Nonibros.
'-----------
' Script Start
'-----------

#dynamic 0x11FFFA0

'---------
' Master Ball Game Function
'---------
#org @masterball
checkflag 0x1100 ' Checks whether we have reached the final object in the order script.
if 0x1 goto @ending ' If we have, stop the script. If not, set up the variables and check.
setvar 0x4002 0X8 'Set our checking variable = the object orders number - 1. Eg. 5th object in line would be 4.
call 0X9244000 ' Call the order checking script offset.
checkflag 0x1100 ' Checks whether we have reached the final object in the order script.
if 0x0 goto @ending ' If we haven't, stop the script. If we have, give the masterball.
goto @givemasterballmain ' Continue on.
end

#org @ending ' This ends the script.
end

'---------
' Master Ball Game Function End
'---------

'---------
' Give Masterball Script
'---------

#org @givemasterballmain
lockall
setweather 0x6
doweather
pause 0x50
fadescreen 0x1
clearflag 0x1110 ' Just for redundancy.
showsprite 0x7 ' Shows DEV.
fadescreen 0x0
msgbox @nonibros 0x6 ' Plays a message telling player that this is the DEV.
textcolor 0x2 ' Sets text to black.
bufferitem 0x1 0x1 ' To [buffer2] Add the item Masterball.
fanfare 0x101
preparemsg @receiveditem ' Received the item Message.
waitmsg
waitfanfare
additem 0x1 0x1 ' Add 1 Masterball.
msgbox @putawayballs 0x6 ' Plays a message telling the player they have put away their balls.
textcolor 0x0 ' Sets color to blue.
msgbox @lookforward 0x6 ' DEV says bye.
fadescreen 0x1
hidesprite 0x7 ' Hide DEV.
setflag 0x1110 ' Tells the game we have completed our events and to remove the DEV.
fadescreen 0x0
pause 0x20
resetweather
doweather
releaseall 'release the player
end

'---------
' Strings
'---------

#org @nonibros
= Nonibros: Hello there [player].\pThanks a lot for playing the\nMasterball Fun Game #1!\pIt was quite fun to make and I\ndo hope you enjoyed.\pYou should look forward to other\nchallenges that await you!\pHere is your Masterball.

#org @receiveditem
= Obtained the [buffer2]!

#org @putawayballs
= [player] put the Masterball away\nin the Bag's Balls Pocket.

#org @lookforward
= I look forward to seeing your\njourney, [player]!

'---------
' Strings End
'---------

'---------
' Give Pokeballs Script End
'---------


'-----------
' Script End
'-----------