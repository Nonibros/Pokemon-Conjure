' For the tilescript in front of the Route boss/old man on Route 2.
' This script will activate a trainer battle with shiny pokemon, after stepping on a tile script.
' In Advance Map, set "Var number:" to "5000" and set "Var value:" to "0000". This means that this will always run. This is designed with a check in place.
' I designed the script so when the player steps on it after defeating the trainer, it will just release them.
' One note. There isn't much I can do about the game not playing the music until the trainer reaches the player. Typically it's instant, but here I have to rely on the trainerbattle command, which auto plays music. If I wanted to change that, I'd need to know ASM, and frankly, I'd probably break the game like this. It's not a big deal, most people won't care anyway whether the music is delayed.
' Copyright @2016 Nonibros
'-----------
' Script Start
'-----------
#dynamic 0x11FFFA0

'-----------
' Tile Specific Variables
'-----------

#org @position1 ' This is placed on the tile closest to the trainer.
setvar 0x4000 0x0
goto @areweactive
end

#org @position2 ' This is placed on the tile #2 in line from the trainer.
setvar 0x4000 0x1
goto @areweactive
end

#org @position3 ' This is placed on the tile #3 in line from the trainer.
setvar 0x4000 0x2
goto @areweactive
end

'-----------
' Tile Specific Variables End
'-----------

'-----------
' Have we battled?
'-----------

#org @areweactive
checkflag 0xE00 ' We check a specific flag reserved for this trainer.
if 0x1 goto @free ' Release the player since we have already battled.
goto @justnoticedyou ' Go to a function for the trainer.
end

#org @free
releaseall
end

'-----------
' Have we battled? End
'-----------

'-----------
' Seeing Player
'-----------

#org @justnoticedyou ' This will lock the player like a regular trainer.
lock
applymovement 0x1 @exclaim ' Exclaimation mark.
waitmovement 0x0
pause 0x8 ' A light pause while the train has an exclamation mark.
goto @specificmovements ' We now move the Trainer to the Player.
end

'-----------
' Seeing Player End
'-----------


'-----------
' Movements Section End
'-----------

#org @specificmovements
compare 0x4000 0x0
if 0x1 goto @mainbattle ' We don't move on the tile next to the trainer, so we will go to the battle function.
compare 0x4000 0x1
if 0x1 call @movement2tile ' This is the 2nd tile from the trainer.
compare 0x4000 0x2
if 0x1 call @movement3tile ' This is the 3rd tile from the trainer.
goto @mainbattle ' Go to the main battle function.
end

'---------
' Movement Functions
'---------

#org @movement2tile
applymovement 0x1 @movementtile2
waitmovement 0x0
return

#org @movement3tile
applymovement 0x1 @movementtile3
waitmovement 0x0
return

'---------
' Movement Functions End
'---------


'-----------
' Movements
'-----------

#org @exclaim
#raw 0x62 'Exclamation Mark (!)
#raw 0xFE 'End of Movements

#org @movementtile2
#raw 0x12 'Step Left (Normal)
#raw 0xFE 'End of Movements

#org @movementtile3
#raw 0x12 'Step Left (Normal)
#raw 0x12 'Step Left (Normal)
#raw 0xFE 'End of Movements

'-----------
' Movements End
'-----------

'-----------
' Movements Section End
'-----------


'-----------
' Battle Section
'-----------

#org @mainbattle
setflag 0xE00
textcolor 0x0 ' Set's text color. 0 for Male - Blue. 1 for Female - Red. 2 for Neutral - Black.
callasm 0x7F81A1 ' Calls Hackmews shiny ASM.
setvar 0x8003 0x102 ' Pokemon to be shiny. Say its 0x905. The 05 would be 5 total trainer Pokemon. The 9 you would convert to binary by the number of pokemon. So, 9 here would be 01001. This says that pokemon 2 and 5 will be shiny. That's all.
trainerbattle 0x0 0x16 0x0 @before @after
end

'-----------
' Battle Section End
'-----------

'---------
' Strings
'---------

#org @before
= Hello young one.\pSince you look ready, we shall\nnow battle!

#org @after
= It seems I lost.\nHahaha.

'---------
' Strings End
'---------


'-----------
' Script End
'-----------