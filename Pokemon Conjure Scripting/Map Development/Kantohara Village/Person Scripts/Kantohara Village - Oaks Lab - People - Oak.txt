'Script for Oak in his lab.
'This one looks like a lot, but there are sections.
'Search for "Start" To find the sections.
'-----------
' Script Start
'-----------

'-----------
' Main Script Start
'-----------
#dynamic 0xOFFSET

'---------
' Main Function
'---------

#org @main
lock
faceplayer
checkflag 0xC00
If 0x1 goto @afteritems ' This can be replaced with a different pointer to an ENTIRLY different script.
'Meaning, you could compile this once, compile a new script elsewhere saying what you would like after talking to Oak, change the offset here to whatever your new script is, re-compile it and VOILA, done. It will then point to the new script after getting the items from oak.
checkflag 0x230
If 0x1 goto @badgreeting
goto @goodgreeting
end

#org @goodgreeting
msgbox @greetingsgood 0x6
goto @givepokemonmain ' give a pokemon.
end

#org @badgreeting
msgbox @greetingsbad 0x6
msgbox @greetingsbad2 0x6 ' Message was long, so split into 2, lol.
goto @givepokedexmain ' Since we are not giving a Pokemon, we will simply give the dex.
end

#org @afteritems
msgbox @goodluck 0x6
release
end

'---------
' Main Function End
'---------

'---------
' Strings
'---------

#org @greetingsgood
= OAK: [player]...\nWe have made a great discovery!\pThis discovery is so great, it\nwill reign in a new era of\lPokemon.\pIt's something greater than we\ncould have ever hoped for.\pConjure.\pIt gives a Pokemon tremondous\npower akin to near immortality.\p[player], I want you to go on a\njourney and do field research on\lConjure Pokemon.\pBut before you leave, I need to\ngive you some things.

#org @greetingsbad
= OAK: I saw you talking with your\nfriend over there, but he didn't\lreally give off a good vibe.\pWhat I mean is he didn't seem to\nlike me.\pIn fact, I could feel hatred\nemanating from him...\pOh, but it seems he gave you a\nPokémon.\pInteresting.\pHe must be an old friend of yours\nI assume?\pWell, that's good, as I was going\nto give you one anyway for an\limportant journey I'd like you\lto go on.

#org @greetingsbad2
= You see, [player], we have made a\ngreat discovery!\pIt's huge, something greater than\nwe could have ever imagined.\pConjure.\pIt gives a Pokémon tremendous\npower akin to near immortality.\p[player], as said before, I want\nyou to go on a journey and do\lfield research on these Conjure\lPokémon.\pNow before you leave with your\nPokémon, I need to give you some\lthings.

#org @goodluck
= Good luck and enjoy your journey\n[player].

'---------
' Strings End
'---------

'-----------
' Main Script End
'-----------

'This is used to give the player a selected Pokemon.
'Variable 0x4003 controls which Pokemon to give, as shown below.
'---------
' Give Pokemon Script Start
'---------
'ADD this in the case of a new person. Since we are just calling this function, dynamic is not needed.
'#dynamic 0xoffset

'---------
' Main Function
'---------

#org @givepokemonmain
' This part is added if you want the NPC to tell the player they have no room.
'countpokemon ' Checks to see if we have 6 pokemon.
'compare 0x800D 0x6
'if 0x1 goto @needroom
msgbox @youllneedapokemon 0x6
setvar 0x4003 0x18B ' Set's variable 0x4003 to Bagon. If you wanted, you could reuse this script exactly as is, for another pokemon, simply by changing this number.
'A NOTE: You need to use the INDEX number of the pokemon, NOT the national dex number. The first 251 pokemon have the same index number as their national number, but after 251, you need to look at the INDEX number.
'I had run into a problem where I was giving out 0x173 = 371, which is the national dex number of bagon. HOWEVER, Bagons INDEX number is 395 = 0x18B. The pokemon with the INDEX number of 371 is Loudred, whose national dex number is 294.
'You can check the index number of a pokemon in Pokemon Game Editor.
call @showpic ' Show a picture of our pokemon.
msgbox @pokemon1message 0x6 ' Display a message telling the player what the pokemon is.
hidepokepic ' Hide the picture.
msgbox @hereyougo 0x6
setflag 0x828 ' Opens pokemon menu, IF NEEDED.
givepokemon 0x4003 0x5 0x0 0x0 0x0 0x0 ' Give a level 5 Bagon.
bufferpokemon 0x0 0x4003 ' To [buffer1] give the value of our variable.
preparemsg @receivedpoke ' Message of "you received X!"
waitmsg
fanfare 0x13E
waitfanfare
msgbox @nicknamemessage 0x5 ' Do you want to nickname the pokemon?
compare LASTRESULT 0x1
if 0x1 call @nicknamepoke
goto @givepokedexmain ' This template is based off of Professor Oaks Speech in his lab, therefore at the end here, we will also give the player a Pokedex.
'goto @ending ' Used if we WEREN'T going to the pokedex.
end

#org @showpic
showpokepic 0x4003 0xA 0x3 ' Shows Picture of Bagon.
textcolor 0x0 ' Sets color to blue.
return

'---------
' Main Function End
'---------

'---------
' No Room Messages
'---------
'Used if this WASN'T the first Pokemon giving, ie. This is the lab. ANYWHERE ELSE and you would NEED this part.
'#org @needroom
'msgbox @noroommessage 0x6
'release
'end

'---------
' No Room Messages End
'---------

'---------
' Nickname
'---------

#org @nicknamepoke ' Counts the final pokemon, and sets that to the one to nickname.
countpokemon
subvar LASTRESULT 0x1
copyvar 0x8004 LASTRESULT
fadescreen 0x1
special 0x9E
waitstate
return

'---------
' Nickname End
'---------

' Used if we WEREN'T going to give the pokedex.
'---------
' Closing
'---------
'#org @ending ' Relock then say goodbye.
'lock
'faceplayer
'msgbox @goodjourney 0x6
'release
'end
'---------
' Closing End
'---------

'---------
' Strings
'---------
' Used for ANY other pokemon giving script.
'#org @noroommessage
'= You have no space.
'
'#org @goodjourney
'= Please treat [buffer1] well, and\nhave fun together!

#org @pokemon1message
= OAK: This Pokémon is called\nBagon.\pIt's a tough Pok\h1bmon to raise,\nbut it's definitely worth it.

#org @hereyougo
= Here you go, [player].

#org @receivedpoke
= [player] received the [buffer1]\nfrom Prof. Oak!

#org @nicknamemessage
= Do you want to give a nickname to\nthis [buffer1]?

#org @youllneedapokemon
= OAK: The first thing you'll need\nis a Pok\h1Bmon.\pThis one is from the Hoenn Region.

'---------
' Strings End
'---------

'---------
' Give Pokemon Script End
'---------

'---------
' Give Pokedex Script Start
'---------

'---------
' Main Function
'---------

#org @givepokedexmain
lock
faceplayer ' Relocks because the player may have nicknamed.
checkflag 0x230
if 0x1 goto @badpokedex
goto @goodpokedex
end

#org @goodpokedex
msgbox @nextitem 0x6 ' Tells the player about the pokedex. Good side.
goto @reallygivepokedex
end

#org @badpokedex
msgbox @baditem 0x6 ' Tells the player about the pokedex. Evil side.
goto @reallygivepokedex
end

#org @reallygivepokedex
applymovement 0xP @faceright ' Oak faces to the right.
waitmovement 0x0
pause 0xA ' Pause a little.
hidesprite 0x5 ' Hides the pokedex.
pause 0xA ' Pause a little.
applymovement 0xP @faceplayer ' Oak faces the player.
waitmovement 0x0
setflag 0x829 ' Set's pokedex.
special 0x16F ' Upgrades to national dex immediately.
fanfare 0x13E ' Fanfare of getting item.
textcolor 0x2 ' Sets color to black.
msgbox @receivedpokedex 0x6 ' Tells the player they have receieved the pokedex.
waitfanfare ' Wait's until fanfare finishes.
goto @givepokeballsmain ' Go to the final script, which is to give pokeballs.
end


'---------
' Main Function End
'---------

'---------
' Strings
'---------

#org @nextitem
= OAK: The next thing I'm going to\ngive you is a blank Pok\h1Bdex.\pI am hoping you can get data on\nmany new Pok\h1Bmon in the field.\pWe currently have a list of about\n330, so I hope you can increase\lthat.\pI also figured it'd be more\ninteresting for you if we just\lcleared the list on this model.\pAnyway, enough rambling, here you\nare!

#org @baditem
= OAK: The first thing I will give\nyou is a blank Pok\h1Bdex.\pI am hoping you can get data on\nmany new Pok\h1Bmon in the field.\pHere you are.

#org @receivedpokedex
= You received a Pok\h1Bdex from\nProf. Oak!

'---------
' Strings End
'---------

'-----------
' Movements
'-----------

#org @faceplayer
#raw 0x4A 'Face Player
#raw 0xFE 'End of Movements

#org @faceright
#raw 0x30 'Face Right (Delayed)
#raw 0xFE 'End of Movements

'-----------
' Movements End
'-----------

'---------
' Give Pokedex Script End
'---------


'---------
' Give Pokeballs Script
'---------

#org @givepokeballsmain
msgbox @finalitem 0x6 ' Plays a message telling player a message before giving pokeballs.
textcolor 0x2 ' Sets text to black.
bufferitem 0x1 0x4 ' To [buffer2] Add the item Poke ball.
fanfare 0x101
preparemsg @receiveditem ' Received the item Message.
waitmsg
waitfanfare
additem 0x4 0x5 ' Add 5 Pokéballs.
msgbox @putawayballs 0x6 ' Plays a message telling the player they have put away their balls.
setflag 0xC00 ' Tells the game we have completed our events.
textcolor 0x0 ' Sets color to blue.
checkflag 0x230 ' Checks bad or good.
if 0x1 goto @adventurebad
goto @adventuregood
end

#org @adventuregood
msgbox @dowellonyouradventuregood 0x6 ' Oak says bye. Good Side.
release 'release the player
end

#org @adventurebad
msgbox @dowellonyouradventurebad 0x6 ' Oak says bye. Bad Side.
release 'release the player
end

'---------
' Strings
'---------

#org @finalitem
= OAK: Now, this is my final gift.\pThese are what are called\nPokéballs.\pThey give you the ability to\ncatch new Pokémon in the field!\pHere you are!

#org @receiveditem
= Obtained the [buffer2]!

#org @putawayballs
= [player] put the Pokéballs away\nin the Bags's Items Pocket.

#org @dowellonyouradventuregood
= Do well on your adventure and\nmake me proud, [player]!

#org @dowellonyouradventurebad
= Be careful of that guy [player].\nHe's a bad character, I can tell.\pAnd of course do well on your\nadventure!


'---------
' Strings End
'---------

'---------
' Give Pokeballs Script End
'---------


'-----------
' Script End
'-----------