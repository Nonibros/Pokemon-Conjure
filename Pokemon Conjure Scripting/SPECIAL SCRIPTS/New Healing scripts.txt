'This script is for regular Non-pokecenter healing after blacking out.
'Currently have bed, and the camper in Route 2 rest stop.
'-----------
' Script Start
'-----------

#dynamic 0x1242000


'-----------
' Pointer
'-----------

#org 0x1A8DD8
goto @nap
end

'-----------
' Pointer End
'-----------

'---------
' Main Function
'---------

#org @nap
lockall
compare 0x500F 0x0
if 0x1 goto @healplayerhouse ' We don't move on the tile next to the trainer, so we will go to the battle function.
compare 0x500F 0x1
if 0x1 goto @healroute2 ' This is the 2nd tile from the trainer.
goto @healplayerhouse ' Autogo to the house incase something is up.

#org @healplayerhouse
textcolor 0x3
applymovement 0xFF @facedown ' We face down towards the bed.
waitmovement 0x0 ' Wait until movements are complete.
msgbox @beforenap MSG_KEEPOPEN
call @healing ' Call healing function
msgbox @afternap MSG_KEEPOPEN
fadedefault
releaseall
end

#org @healroute2
lockall
textcolor 0x0
applymovement 0xFF @faceup ' We face up towards the camper.
waitmovement 0x0 ' Wait until movements are complete.
msgbox @beforehealroute2 MSG_KEEPOPEN
call @healing ' Call healing function
msgbox @afterhealroute2 MSG_KEEPOPEN
fadedefault
releaseall
end

'---------
' Main Function End
'---------


'---------
' Healing Function
'---------

#org @healing
fadescreen 0x1
fanfare 0x100
waitfanfare
special 0x0
fadescreen 0x0
return

'---------
' Healing Function End
'---------

'---------
' Strings
'---------

#org @beforenap
= You finally make to to the house\nafter running from the battle.\pYou're very exhausted.\pWell, maybe a nap will help...

#org @afternap
= That was a fantastic rest!\pBoth you and your Pok\h1Bmon are\nmuch better from that nap!\pWell, It's time to go now!

#org @beforehealroute2
= Hey, kid, kid, are you ok?\nJust rest a bit, I'll help you.

#org @afterhealroute2
= Alright kiddo, you seem to be all\nbetter.\pI used to work as a doctor, but\nnow I manage the public areas.\pFarewell and be careful!

'---------
' Strings End
'---------

'---------
' Movements
'---------

#org @facedown
#raw 0x0  ' Face Down
#raw 0xFE ' End of Movements

#org @faceup
#raw 0x1  ' Face Up
#raw 0xFE ' End of Movements

'---------
' Movements End
'---------



'-----------
' Script End
'-----------