' This script is for regular Non-pokecenter healing after blacking out.
' Currently have bed, and the camper in Route 2 rest stop.
' Script made by: Nonibros
'-----------
' Script Start
'-----------

#dynamic 0x1242000


'-----------
' Pointer
'-----------

#org 0x1A8DD8 ' We will edit the non - PC healing spot when we die to point to our main function.
goto @whichspot
end

'-----------
' Pointer End
'-----------

'---------
' Main Function
'---------

#org @whichspot ' When we lost all of our Pokemon, we will pop 
lockall
compare 0x500F 0x0 ' This is set in the players house. When they die, they appear home in bed.
if 0x1 goto @healplayerhouse ' Go to the players house.
compare 0x500F 0x1 ' This is set once the player enters Route 2. When they die, they appear by the camper in the rest stop one Route 2.
if 0x1 goto @healroute2 ' Go to the rest house on Route 2.
goto @healplayerhouse ' Auto-go to the house in case something is up.

#org @healplayerhouse ' Players house function.
textcolor 0x3 ' Black text because you are talking.
applymovement 0xFF @facedown ' We face down towards the bed.
waitmovement 0x0 ' Wait until movements are complete.
msgbox @beforenap MSG_KEEPOPEN ' Message before healing.
call @healing ' Call healing function
msgbox @afternap MSG_KEEPOPEN ' Message after healing.
fadedefault ' Fade out after fainting.
releaseall ' Release the player so they can walk and stuff.
end

#org @healroute2 ' Rest stop on Route 2 function.
textcolor 0x0 ' Blue text because you are talking.
applymovement 0xFF @faceup ' We face up towards the camper.
waitmovement 0x0 ' Wait until movements are complete.
msgbox @beforehealroute2 MSG_KEEPOPEN ' Message before healing.
call @healing ' Call healing function
msgbox @afterhealroute2 MSG_KEEPOPEN ' Message after healing.
fadedefault ' Fade out after fainting.
releaseall ' Release the player so they can walk and stuff.
end

'---------
' Main Function End
'---------


'---------
' Healing Function
'---------

#org @healing ' This is the healing function.
fadescreen 0x1
fanfare 0x100
waitfanfare
special 0x0
fadescreen 0x0
return

'---------
' Healing Function End
'---------

'---------
' Strings
'---------

#org @beforenap
= You finally make to to the house\nafter running from the battle.\pYou're very exhausted.\pWell, maybe a nap will help...

#org @afternap
= That was a fantastic rest!\pBoth you and your Pok\h1Bmon are\nmuch better from that nap!\pWell, It's time to go now!

#org @beforehealroute2
= Hey, kid, kid, are you ok?\nJust rest a bit, I'll help you.

#org @afterhealroute2
= Alright kiddo, you seem to be all\nbetter.\pI used to work as a doctor, but\nnow I manage the public areas.\pFarewell and be careful!

'---------
' Strings End
'---------

'---------
' Movements
'---------

#org @facedown
#raw 0x0  ' Face Down
#raw 0xFE ' End of Movements

#org @faceup
#raw 0x1  ' Face Up
#raw 0xFE ' End of Movements

'---------
' Movements End
'---------



'-----------
' Script End
'-----------